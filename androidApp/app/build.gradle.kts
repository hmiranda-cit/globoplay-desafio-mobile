/* Copyright Â© 2024 CI&T. All Rights Reserved.
 * Unauthorized copying of this file's contents, via any medium, is strictly prohibited
 * Proprietary
 * Written by Henrique Miranda <hmiranda@ciandt.com>
 */

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.hiddenSecrets)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.parcelize)
}

android {
    val applicationIdentifier = "com.ciandt.globo.tmdb.app"
    val applicationVersion = 1
    val javaJvmVersion = "1.8"
    val latestSdkVersion = 35

    namespace = applicationIdentifier
    compileSdk = latestSdkVersion

    defaultConfig {
        applicationId = applicationIdentifier
        minSdk = 21
        targetSdk = latestSdkVersion
        versionCode = applicationVersion
        versionName = "$applicationVersion"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildFeatures {
        compose = true
    }

    buildTypes {
        debug {
            isDebuggable = true
            isMinifyEnabled = false
        }
    }

    compileOptions {
        sourceCompatibility(javaJvmVersion)
        targetCompatibility(javaJvmVersion)
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.15" // Depends on kotlin version at libs.version.toml. See https://developer.android.com/jetpack/androidx/releases/compose-kotlin
    }

    // Required by hiddensecrets plugin
    externalNativeBuild {
        cmake {
            path = layout.projectDirectory.file("src/main/cpp/CMakeLists.txt").asFile
        }
    }

    kotlinOptions {
        jvmTarget = javaJvmVersion
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation(platform(libs.androidx.compose.bom))
    debugImplementation(platform(libs.androidx.compose.bom))
    implementation(platform(libs.coil.bom))

    implementation(libs.androidx.activity)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.compose.foundation)
    implementation(libs.androidx.compose.material)
    implementation(libs.androidx.compose.preview)
    implementation(libs.androidx.compose.runtime)
    implementation(libs.androidx.core)
    implementation(libs.androidx.core.splashscreen)
    implementation(libs.coil.compose)
    implementation(libs.coil.network)
    implementation(libs.okhttp.logging)

    // Required by code generated by the openapi plugin
    implementation(libs.moshi.adapters)
    implementation(libs.moshi.kotlin)
    implementation(libs.okhttp)

    debugImplementation(libs.androidx.compose.preview.debug)
}
